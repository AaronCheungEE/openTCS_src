apply plugin: 'application'

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.guing.RunPlantOverview'
}
mainClassName = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')
ext.appDir = new File(collectableDistDir, project.name)

sourceSets {
  guiceConfig
}

configurations {
  guiceConfigCompile.extendsFrom compile
}

dependencies {
  compile project(':openTCS-Base')
  compile group: 'com.google.inject', name: 'guice', version: '3.0'
  compile group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: '3.0'
  compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '3.0'
  compile group: 'net.engio', name: 'mbassador', version: '1.1.10'
  // There does not seem to be an official binary release for JHotDraw...
  compile files(rootProject.file('lib/jhotdraw-7.6.jar'))
  // This preview version of Docking Frames is not in Maven Central, yet.
  compile files(rootProject.file('lib/docking-frames-common-1.1.2p11.jar'))
  compile files(rootProject.file('lib/docking-frames-core-1.1.2p11.jar'))
  
  guiceConfigCompile sourceSets.main.output
}

compileJava {
  options.compilerArgs << "-Xlint:all"
  options.compilerArgs << "-Xlint:-serial"
  options.compilerArgs << "-Xlint:-rawtypes"
  options.compilerArgs << "-Xlint:-unchecked"
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
}

jar {
  from sourceSets.guiceConfig.output
  // This merely tells NetBeans where to look for classes in case other
  // subprojects depending on this one. By default, it only scans 'main'.
  ext.netBeansSourceSets = [sourceSets.guiceConfig, sourceSets.main]
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installApp
}

run {
  dependsOn installApp
  workingDir appDir
  enableAssertions true
  classpath(files { new File(appDir, 'lib').listFiles() })
  systemProperties(['java.util.logging.config.file':'./config/logging-plantoverview.config',\
                    'java.security.policy':'file:./config/java.policy',\
                    'sun.java2d.d3d':'false',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.kernel.host':'localhost',\
                    'opentcs.kernel.port':'1099',\
                    'opentcs.dockinglayout':'./config/plantoverview-docking-layout.xml',\
                    'org.opentcs.util.configuration.xml.file':'./config/openTCS-config.xml',\
                    'org.opentcs.util.configuration.saveonexit':'true'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow',\
          '-splash:bin/splash-image.gif')
}

task(debug, type:JavaExec) {
  dependsOn installApp
  workingDir run.workingDir
  enableAssertions run.enableAssertions
  
  main run.main
  args run.args
  classpath run.classpath
  jvmArgs run.jvmArgs
  systemProperties run.systemProperties

  debug true
}
